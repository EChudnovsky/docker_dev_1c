#!/usr/bin/env executor

#требуется СтруктурыДанных.sbsl
#требуется ПроцессыОС.sbsl


@Глобально
структура КомандаCLI
    обз знч Имя: Строка
    пер ОписаниеКраткое: Строка
    пер Описание: Строка

    пер ПараметрыКоманды: Массив<Строка>
    пер ПараметрыСкрипта: Строка
    пер ПараметрыВыполнения: СтруктурыДанных.ПараметрыВыполнения?
    пер ВремяОжиданияВыполнения = 60мс

    пер ПотокВывода: Массив<Строка>
    пер ПотокОшибок: Массив<Строка>
    пер Отказ: Булево

    пер ВыводПомощи: Булево
    
    @ИменованныеПараметры
    конструктор

    @Глобально
    метод Инициализация()
        если ПараметрыВыполнения == Неопределено
            ПараметрыВыполнения = новый СтруктурыДанных.ПараметрыВыполнения()
            ПараметрыВыполнения.Окружение.Инициализация()
            ПараметрыВыполнения.ЖурналЛогирования.Инициализация()
        ;
    ;

    @Глобально
    метод ДобавитьПараметрКоманды(ИмяПараметра: Строка, Значение1:Строка,
                                                        Значение2:Строка = "",
                                                        Значение3:Строка = "", 
                                                        Значение4:Строка = "", 
                                                        Значение5:Строка = "", 
                                                        Значение6:Строка = "",
                                                        Значение7:Строка = "",
                                                        Значение8:Строка = "",
                                                        Значение9:Строка = "")
        ПараметрыКоманды.Добавить(ИмяПараметра)
        
        если не Значение1.Пусто()
            ПараметрыКоманды.Добавить(Значение1)
        ;
        если не Значение2.Пусто()
            ПараметрыКоманды.Добавить(Значение2)
        ;
        если не Значение3.Пусто()
            ПараметрыКоманды.Добавить(Значение3)
        ;
        если не Значение4.Пусто()
            ПараметрыКоманды.Добавить(Значение4)
        ;
        если не Значение5.Пусто()
            ПараметрыКоманды.Добавить(Значение5)
        ;
        если не Значение6.Пусто()
            ПараметрыКоманды.Добавить(Значение6)
        ;
        если не Значение7.Пусто()
            ПараметрыКоманды.Добавить(Значение7)
        ;
        если не Значение8.Пусто()
            ПараметрыКоманды.Добавить(Значение8)
        ;
        если не Значение9.Пусто()
            ПараметрыКоманды.Добавить(Значение9)
        ;
    ;

    @Глобально
    метод УстановитьТихийРежим(ТихийРежим: Булево)
        ПараметрыВыполнения.ЖурналЛогирования.ТихийРежим = ТихийРежим
    ;

    @Глобально
    метод ТихийРежим(): Булево
        возврат ПараметрыВыполнения.ЖурналЛогирования.ТихийРежим
    ;

    @Глобально
    метод ОчиститьПотоки(ОчищатьПотокВывода: Булево = Истина, ОчищатьПотокОшибок: Булево = Истина)
        
        если ОчищатьПотокВывода
            ПотокВывода.Очистить()
        ;

        если ОчищатьПотокОшибок
            ПотокВывода.Очистить()
        ;
    ;

    @Глобально
    метод ВсеПотокиВСтроку(): Строка
        возврат 
        "${ПотокВыводаВСтроку()}
        ${ПотокОшибокВСтроку()}"
    ;

    @Глобально
    метод ПотокВыводаВСтроку(): Строка
        возврат Коллекции.ВставитьПараметрыВСтроку("${Строки.Соединить(ПотокВывода, "\в\н")}", ПараметрыВыполнения.Окружение)
    ;

    @Глобально
    метод ПотокОшибокВСтроку(): Строка
        возврат Коллекции.ВставитьПараметрыВСтроку("${Строки.Соединить(ПотокОшибок, "\в\н")}", ПараметрыВыполнения.Окружение)
    ;

    @Глобально
    метод ДобавитьСообщение(Сообщение: Строка|Массив<Строка>, Замещать: Булево = Ложь)

        ОчиститьПотоки(Замещать, Ложь)
        если СтроковыеФункции.ПустаяСтрока(Сообщение)
            возврат
        ; 

        выбор Сообщение
        когда это Строка
            ПотокВывода.Добавить(Сообщение как Строка)
        иначе
            ПотокВывода.ДобавитьВсе(Сообщение как Массив<Строка>)
        ;
    ;
    
    @Глобально
    метод ДобавитьОшибку(Сообщение: Строка|Массив<Строка>, Замещать: Булево = Ложь)

        ОчиститьПотоки(Ложь, Замещать)
        если СтроковыеФункции.ПустаяСтрока(Сообщение)
            возврат
        ; 

        выбор Сообщение
        когда это Строка
            ПотокОшибок.Добавить(Сообщение как Строка)
        иначе
            ПотокОшибок.ДобавитьВсе(Сообщение как Массив<Строка>)
        ;
    ;

    @Глобально
    метод ВывестиВсеПотоки(ОчищатьПоток: Булево = Истина, ВременнаяМетка: Булево = Истина)
        ВывестиПотокВывода(ОчищатьПоток, ВременнаяМетка)
        ВывестиПотокОшибок(ОчищатьПоток)
    ;

    @Глобально
    метод ВывестиПотокВывода(ОчищатьПоток: Булево = Истина, ВременнаяМетка: Булево = Истина)

        если ПотокВывода.Размер() == 0
            возврат
        ; 

        пер СостояниеТихогоРежима = ТихийРежим()
        УстановитьТихийРежим(Ложь)

        ПараметрыВыполнения.ЖурналЛогирования.Информация(
            "${ПотокВыводаВСтроку()}",
            ВременнаяМетка)

        УстановитьТихийРежим(СостояниеТихогоРежима)
        
        ОчиститьПотоки(ОчищатьПоток, Ложь)

    ;

    @Глобально
    метод ВывестиПотокОшибок(ОчищатьПоток: Булево = Истина)

        если ПотокОшибок.Размер() == 0
            возврат
        ; 

        пер СостояниеТихогоРежима = ТихийРежим()
        УстановитьТихийРежим(Ложь)

        ПараметрыВыполнения.ЖурналЛогирования.Ошибка(
            "${ПотокОшибокВСтроку()}")
        
        УстановитьТихийРежим(СостояниеТихогоРежима)

        ОчиститьПотоки(Ложь, ОчищатьПоток)

    ;

    @Глобально
    метод ВывестиСообщение(ТекстСообщения: Строка, ВременнаяМетка: Булево = Истина)

        если ТекстСообщения.Пусто()
            возврат
        ; 

        пер СостояниеТихогоРежима = ТихийРежим()
        УстановитьТихийРежим(Ложь)
        ПараметрыВыполнения.ЖурналЛогирования.Информация(ТекстСообщения, ВременнаяМетка)
        УстановитьТихийРежим(СостояниеТихогоРежима)
    ;

    @Глобально
    метод ВывестиОтладочноеСообщение(ТекстСообщения: Строка, ВременнаяМетка: Булево = Истина)

        если ТекстСообщения.Пусто()
            возврат
        ; 

        пер СостояниеТихогоРежима = ТихийРежим()
        УстановитьТихийРежим(Ложь)
        ПараметрыВыполнения.ЖурналЛогирования.Отладка(ТекстСообщения, ВременнаяМетка)
        УстановитьТихийРежим(СостояниеТихогоРежима)
    ;

;

// ПРОГРАММНЫЙ ИНТЕРФЕЙС
@Глобально
метод СоздатьКоманду(ИмяКоманды: Строка, ОписаниеКраткое: Строка = "", Описание: Строка = "", ПараметрыСкрипта: Строка = ""): КомандаCLI
    знч КомандаCLI = новый КомандаCLI(
        Имя = ИмяКоманды.Сократить().ВНижнийРегистр(),
        ПараметрыСкрипта = ПараметрыСкрипта,
        ОписаниеКраткое = ОписаниеКраткое,
        Описание = Описание
    )
    
    КомандаCLI.Инициализация()

    возврат КомандаCLI

;

@Глобально
метод ПередНачаломВыполненияКомандыCli(КомандаCLI: КомандаCLI)
    
    КомандаCLI.ВывестиСообщение("\в\нНАЧАЛО КОМАНДЫ: ${КомандаCLI.ОписаниеКраткое}")
    КомандаCLI.ДобавитьСообщение(КомандаCLI.ВсеПотокиВСтроку(), Истина)
    КомандаCLI.ВывестиВсеПотоки()
;

@Глобально
метод ВыполнитьКомандуCli(КомандаCLI: КомандаCLI): Соответствие<Строка, Строка|Булево>
    
    пер РезультатКоманды = {"Результат": "", "Отказ": Ложь, "ОписаниеОшибки": "" }

    если КомандаCLI.Отказ
        КомандаCLI.ВывестиОтладочноеСообщение("На начало выполнения, команда имела статус ОТКАЗ. Вызов команды прерван.")

        УстановитьРезультатКоманды(КомандаCLI, РезультатКоманды)
        возврат РезультатКоманды
    ;
    
    пер СостояниеТихогоРежима = КомандаCLI.ТихийРежим()
    
    попытка 
        
        // Заполнение параметров команды
        
        КомандаCLI.Описание = ПолучитьСтрокуДляВывода(КомандаCLI.Описание, КомандаCLI)
        КомандаCLI.ОписаниеКраткое = ПолучитьСтрокуДляВывода(КомандаCLI.ОписаниеКраткое, КомандаCLI)

        пер ПараметрыКоманды = ПараметыВыполенияКоманды(КомандаCLI)
        
        если КомандаCLI.ВыводПомощи
            КомандаCLI.УстановитьТихийРежим(Истина)
        ;

        // Запуск процесса 
        пер Процесс = новый ПроцессыОС.ОписаниеПроцессаОС(
            Команда = "1cedtcli",
            Параметры = ПараметрыКоманды,
            ВремяОжидания = КомандаCLI.ВремяОжиданияВыполнения,
            ЖурналЛогирования = КомандаCLI.ПараметрыВыполнения.ЖурналЛогирования)
        
        Процесс.Запустить()

        КомандаCLI.Отказ        = Процесс.КодВозврата != 0 
        КомандаCLI.ДобавитьСообщение(Процесс.ПотокВывода)
        КомандаCLI.ДобавитьОшибку(Процесс.ПотокОшибок)
        если Процесс.КодВозврата != 0
            выбросить новый ИсключениеНедопустимоеСостояние("ПРОЦЕСС ВЕРНУЛ КОД ОШИБКИ: ${Процесс.КодВозврата}")
        ;
    
    поймать ИнформацияОбОшибке: Исключение
        
        КомандаCLI.Отказ = Истина
        КомандаCLI.УстановитьТихийРежим(Ложь)
        КомандаCLI.ВывестиВсеПотоки()
        выбросить новый ИсключениеНедопустимоеСостояние(
            "В ходе вызова команды CLI ${КомандаCLI.Имя} возникли критические ошибки:
            ${ИнформацияОбОшибке.Представление()}
            Выполнение прервано")
    ;
    
    КомандаCLI.УстановитьТихийРежим(СостояниеТихогоРежима)

    УстановитьРезультатКоманды(КомандаCLI, РезультатКоманды)
    КомандаCLI.ВывестиВсеПотоки(Истина, Ложь)

    возврат РезультатКоманды

;

метод УстановитьРезультатКоманды(КомандаCLI: КомандаCLI, РезультатКоманды: Соответствие<Строка, Строка|Булево>)

    пер СтрокаРезультата = КомандаCLI.ПотокВыводаВСтроку()
    пер СтрокаОшибки     = КомандаCLI.ПотокОшибокВСтроку()
    пер Отказ            = КомандаCLI.Отказ или не СтроковыеФункции.ПустаяСтрока(СтрокаОшибки) 

    выбор 
    когда Отказ
        СтрокаРезультата = ""
    когда КомандаCLI.ВыводПомощи
        ВывестиСправку(КомандаCLI)
        СтрокаРезультата = КомандаCLI.ПотокВыводаВСтроку()
    когда СтрокаРезультата.Найти("Ошибка взаимодействия с платформой") > 0
        СтрокаОшибки = СтрокаРезультата
        Отказ        = Истина
    иначе
        РезультатКоманды["Результат"] = СтрокаРезультата.Сократить()
    ;

    РезультатКоманды["ОписаниеОшибки"] = СтрокаОшибки
    РезультатКоманды["Результат"]      = Отказ ? "" : СтрокаРезультата
    РезультатКоманды["Отказ"]          = Отказ

;

@Глобально
метод ПослеВыполненияКомандыCli(КомандаCLI: КомандаCLI)
    
    КомандаCLI.ВывестиВсеПотоки()
    КомандаCLI.ВывестиСообщение("ЗАВЕРШЕНИЕ КОМАНДЫ: ${КомандаCLI.Отказ ? "ОШИБКА" : "УСПЕХ"}")
;


метод ВывестиСправку(КомандаCLI: КомандаCLI)
    
    пер СтрокаПомощи = Строки.Соединить(КомандаCLI.ПотокВывода).Сократить()
    
    если СтрокаПомощи == "false"
        СтрокаПомощи = "<Справка отсутствует>"
    ;
    
    КомандаCLI.ДобавитьСообщение(
    "СПРАВКА ПО КОМАНДЕ:
    ${КомандаCLI.Имя}(1)    Пользовательские команды EDT    ${КомандаCLI.Имя}(1)

    ОПИСАНИЕ
    ${КомандаCLI.Описание}
    
    $СтрокаПомощи", Истина)

;


@Локально
метод ПараметыВыполенияКоманды(КомандаCLI: КомандаCLI): Массив<Строка>

    знч Окружение = КомандаCLI.ПараметрыВыполнения.Окружение
    пер ПараметрыКоманды: Массив<Строка>
    
    ПараметрыКоманды.Добавить("-data")
    ПараметрыКоманды.Добавить("\"${Окружение.EDT_PATH_WORKSPACE.ЗначениеСКонтролем()}\"")
    
    если Окружение.EDT_MAXTIMEOUT.Заполнено
        ПараметрыКоманды.Добавить("-timeout")
        ПараметрыКоманды.Добавить("${Окружение.EDT_MAXTIMEOUT.Значение}")
    ;

    если Окружение.EDT_VMARGS.Заполнено
        ПараметрыКоманды.Добавить("-vmargs")
        ПараметрыКоманды.Добавить("${Окружение.EDT_VMARGS.Значение}")
    ;

    если не КомандаCLI.Имя.Пусто()
        ПараметрыКоманды.Добавить("-command")
        ПараметрыКоманды.Добавить("${КомандаCLI.Имя}")
    ;

    КомандаCLI.ВыводПомощи = ("help" == КомандаCLI.Имя)
    если КомандаCLI.ПараметрыСкрипта.Пусто()
        
        для ЗначениеПараметра из КомандаCLI.ПараметрыКоманды
            
            если ЕстьПараметрПомощи(ЗначениеПараметра)
                КомандаCLI.ВыводПомощи = Истина
            ;
            ПараметрыКоманды.Добавить("${Коллекции.ВставитьПараметрыВСтроку(ЗначениеПараметра, Окружение)}")
        ;

    иначе

        КомандаCLI.ВыводПомощи = КомандаCLI.ВыводПомощи или ЕстьПараметрПомощи(КомандаCLI.ПараметрыСкрипта)
        для ЗначениеПараметра из КомандаCLI.ПараметрыСкрипта.Разделить(" ", Ложь)
            ПараметрыКоманды.Добавить(ЗначениеПараметра.Сократить())
        ;
    ;
    
    возврат ПараметрыКоманды
;
метод ЕстьПараметрПомощи(Параметры: Строка): Булево
    
    пер МассивСлов = Параметры.Разделить(" ", Ложь )
    для Слово из МассивСлов

        если Слово.Найти("-h") > 0 
            или Слово.Найти("--help") > 0 
            возврат Истина

        ;    
    ;
    возврат Ложь
;

@Глобально
метод ПолучитьСтрокуДляВывода(Шаблон: Строка|Массив<Строка>, Окружение: СтруктурыДанных.ПараметрыОкружения): Строка
    выбор Шаблон
    когда это Строка
        возврат Коллекции.ВставитьПараметрыВСтроку((Шаблон как Строка), Окружение)
    иначе
        возврат Коллекции.ВставитьПараметрыВСтроку(Строки.Соединить((Шаблон как Массив<Строка>), "\в\н"), Окружение)
    ;
    
;
@Глобально
метод ПолучитьСтрокуДляВывода(Шаблон: Строка|Массив<Строка>, КомандаCLI: КомандаCLI): Строка
    возврат ПолучитьСтрокуДляВывода(Шаблон, КомандаCLI.ПараметрыВыполнения.Окружение)
;