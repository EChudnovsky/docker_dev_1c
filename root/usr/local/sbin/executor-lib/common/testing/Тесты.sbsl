#требуется ../Логирование.sbsl

@Глобально
структура ОписаниеТестов
    пер СписокТестов: Соответствие<Строка, Массив<ОписаниеТеста>>
    пер СписокСкриптов: Соответствие<Строка, Массив<ОписаниеТеста>>
    пер ЖурналЛогирования: Логирование.ЖурналЛогирования
    
    @Глобально
    метод ДобавитьТест(ФайлТестовогоСкрипта: Строка, ТестовыйМетод: Строка, ОписаниеТеста: Строка = ""): ОписаниеТестов
    
        пер НовыйТест = новый ОписаниеТеста(
            ТестовыйМетод = ТестовыйМетод,
            ОписаниеТеста = ОписаниеТеста,
            ФайлТестовогоСкрипта = ФайлТестовогоСкрипта
        )

        // Общий список тестов
        если СписокТестов.ПолучитьИлиНеопределено(ТестовыйМетод) == Неопределено
            СписокТестов.Вставить(ТестовыйМетод, [  ])
        ;
        СписокТестов[ТестовыйМетод].Добавить(НовыйТест)

        // Общий список тестов по скриптам
        если СписокСкриптов.ПолучитьИлиНеопределено(ФайлТестовогоСкрипта) == Неопределено
            СписокСкриптов.Вставить(ФайлТестовогоСкрипта, [  ])
        ;
        
        СписокСкриптов[ФайлТестовогоСкрипта].Добавить(НовыйТест)

        возврат этот

    ;
    
    метод ДобавитьТестВНабор(ФайлТестовогоСкрипта: Строка, Тест: ОписаниеТеста)
        
        если СписокСкриптов.ПолучитьИлиНеопределено(ФайлТестовогоСкрипта) == Неопределено
            СписокСкриптов.Вставить(ФайлТестовогоСкрипта, [  ])
        ;
        СписокСкриптов[ФайлТестовогоСкрипта].Добавить(Тест)
    ;

    @Локально
    метод НайтиТест(ТестовыйМетод: Строка): Массив<ОписаниеТеста>?
        возврат СписокТестов.ПолучитьИлиНеопределено(ТестовыйМетод)
    ;

    @Локально
    метод НайтиНаборТестов(ФайлТестовогоСкрипта: Строка): Массив<ОписаниеТеста>?

        возврат СписокСкриптов.ПолучитьИлиНеопределено(ФайлТестовогоСкрипта)
    ;

    метод Инициализация()
        ЖурналЛогирования = новый Логирование.ЖурналЛогирования(УровеньЛогирования = Логирование.УровниЛогирования.DEBUG)
        ЖурналЛогирования.Инициализация("", "")
    ;

    @Глобально
    метод Тестировать(ИменаТестов: Строка|Массив<Строка>? = Неопределено)
        
        пер СписокИмен: Массив<Строка>?
        выбор ИменаТестов
        когда это Неопределено
            ТестироватьНабор()
        когда это Строка
            СписокИмен = (ИменаТестов как Строка).Разделить(";", Ложь)
        когда это Массив<Строка>
            СписокИмен = ИменаТестов как Массив<Строка>
        ;

        Инициализация()

        пер НаборТестовДляВыполнения: Соответствие<Строка, Массив<ОписаниеТеста>>
        для ИмяТеста из СписокИмен
            пер НаборТестов = НайтиТест(ИмяТеста)
            если НаборТестов == Неопределено
                ЖурналЛогирования.Отладка("[SKIP] Не удалось найти тест с именем <$ИмяТеста>. Тест пропущен.")
                продолжить
            ;
            для Тест из НаборТестов
                ДобавитьТестВНабор(Тест.ФайлТестовогоСкрипта, Тест)
            ;
        ;

        ТестироватьНабор(НаборТестовДляВыполнения)
    ;

    @Глобально
    метод ТестироватьНабор(ИменаНаборов: Строка|Массив<Строка>|Соответствие<Строка, Массив<ОписаниеТеста>>? = Неопределено)
        
        Инициализация()

        пер СписокИменНаборовТеста: Массив<Строка>
        пер НаборыДляТестирования: Соответствие<Строка, Массив<ОписаниеТеста>>

        выбор ИменаНаборов
        когда это Неопределено
            НаборыДляТестирования = СписокСкриптов
        когда это Соответствие<Строка, Массив<ОписаниеТеста>>
            НаборыДляТестирования = ИменаНаборов как Соответствие<Строка, Массив<ОписаниеТеста>>
        когда это Строка
            СписокИменНаборовТеста.ДобавитьВсе((ИменаНаборов как Строка).Разделить(",", Ложь))
        когда это Массив<Строка>
            СписокИменНаборовТеста = ИменаНаборов как Массив<Строка>
        ;

        для ФайлТестовогоСкрипта из СписокИменНаборовТеста
            пер ТекущийНабор = СписокСкриптов.ПолучитьИлиНеопределено(ФайлТестовогоСкрипта)
            если ТекущийНабор == Неопределено
                ЖурналЛогирования.Отладка("[SKIP] Не удалось найти скрипт с тестами по пути <$ФайлТестовогоСкрипта>. Набор пропущен.")
                продолжить
            ;

            НаборыДляТестирования.Вставить(ФайлТестовогоСкрипта, ТекущийНабор)

        ;

        для ТекущийНабор из НаборыДляТестирования
            
            пер ПутьКФайлуСкрипта = ТекущийНабор.Ключ
            ЖурналЛогирования.Информация("ТЕСТОВЫЙ НАБОР: <${ПутьКФайлуСкрипта}>\в\н")
            пер ПараметрыЗапуска = ["-l", "ru", "--dev-lang", "ru" ]
            // , "--debug-port", "${DEBUG_PORT}"]
            для Индекс = 0 по ТекущийНабор.Значение.Граница()
                пер ТекущийТест = ТекущийНабор.Значение[Индекс]
                
                ЖурналЛогирования.Информация("ТЕСТ: ${ТекущийТест.ТестовыйМетод}")
                пер АргументыЗапуска = новый Массив(ПараметрыЗапуска)
                АргументыЗапуска.Добавить("${ПутьКФайлуСкрипта}::${ТекущийТест.ТестовыйМетод}") 
                пер Процесс = новый ПроцессОс(
                    Команда = "executor",
                    Аргументы = АргументыЗапуска,
                    СоединитьПотокиОшибокИВывода = Истина)
                
                Процесс.Запустить()

                исп РезультатПотокаВывода = Процесс.ПолучитьПотокВывода()
                пока не Процесс.ОжидатьЗавершения(60с)
                    ЖурналЛогирования.Информация(РезультатПотокаВывода.ПрочитатьКакСтроку(), Ложь)
                ;
                
                ЖурналЛогирования.Информация(РезультатПотокаВывода.ПрочитатьКакСтроку(), Ложь)

                если Процесс.ПолучитьКодВозврата() == 0
                    ЖурналЛогирования.Информация("ТЕСТ ЗАВЕРШЕН")
                иначе
                    ЖурналЛогирования.Ошибка("ТЕСТ ЗАВЕРШЕН")
                ;
            ;
        ;
    ;

    @Глобально
    метод Запустить(ИмяНабораИлиТеста: Строка = "")
        выбор
            когда ИмяНабораИлиТеста.Пусто()
                ТестироватьНабор()
            когда НайтиНаборТестов(ИмяНабораИлиТеста) != Неопределено
                ТестироватьНабор(ИмяНабораИлиТеста)
            когда НайтиТест(ИмяНабораИлиТеста) != Неопределено
                Тестировать(ИмяНабораИлиТеста)
            иначе
                выбросить новый ИсключениеНедопустимыйАргумент("Не удалось найти тест для выполнения: $ИмяНабораИлиТеста")
            ;
    ;
;

структура ОписаниеТеста
    обз знч ТестовыйМетод: Строка
    обз знч ОписаниеТеста: Строка
    обз знч ФайлТестовогоСкрипта: Строка
;
