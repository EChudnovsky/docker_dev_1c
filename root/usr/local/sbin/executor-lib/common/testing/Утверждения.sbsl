@Глобально
структура assert
    
    пер ОбъектПроверки: ОписаниеЗначения
    // пер ТестирумыйМетод: (неизвестно)->ничто|Неопределено = Неопределено
    пер ПрефиксОшибки: Строка


    @Глобально
    метод Что(Значение: Объект?, Сообщение: Строка = ""): assert
        
        СброситьКонтекст()
        ОбъектПроверки.Значение(Значение)
        ПрефиксОшибки = Сообщение
        возврат этот
    ;

    @Глобально
    метод Свойство(ИмяСвойства: Строка): assert
        
        ОбъектПроверки.Свойство(ИмяСвойства)
        возврат этот        
    ;

    @Глобально
    метод Равно(ОжидаемоеЗначение: Объект, ОписаниеПроверки:Строка? = Неопределено): assert
        если (ОбъектПроверки.Значение() != ОжидаемоеЗначение) 
            ВыброситьИсключение("Значение не удовлетворяет условию проверки <${ОбъектПроверки.Значение()}> равно <$ОжидаемоеЗначение>.", ОписаниеПроверки)
        ;
        возврат этот        
    ;

    @Глобально
    метод НеРавно(ОжидаемоеЗначение: Объект, ОписаниеПроверки:Строка? = Неопределено): assert
        
        если (ОбъектПроверки.Значение() == ОжидаемоеЗначение) 
            ВыброситьИсключение("Значение не удовлетворяет условию проверки <${ОбъектПроверки.Значение()}> равно <$ОжидаемоеЗначение>.", ОписаниеПроверки)
        ;
        возврат этот
    ;

    @Глобально
    метод Заполнено(ОписаниеПроверки:Строка? = Неопределено): assert
        
        пер Результат = Утверждения.ЗначениеЗаполнено(ОбъектПроверки.Значение())
        если (не Результат) 
            ВыброситьИсключение("Значение не удовлетворяет условию проверки <ЗАПОЛНЕНО>.", ОписаниеПроверки)
        ;

        возврат этот
    ;

    @Глобально
    метод НеЗаполнено(ОписаниеПроверки:Строка? = Неопределено): assert
        
        пер Результат = Утверждения.ЗначениеЗаполнено(ОбъектПроверки.Значение())
        если (Результат) 
            ВыброситьИсключение("Значение не удовлетворяет условию проверки <НЕЗАПОЛНЕНО>.", ОписаниеПроверки)
        ;

        возврат этот
    ;

    метод СброситьКонтекст()

    ;

    метод ВыброситьИсключение(Сообщение: Строка, ОписаниеПроверки:Строка? = Неопределено)
        пер ТекстОшибки = "$ПрефиксОшибки: $Сообщение" + (ОписаниеПроверки == Неопределено ? "": "\в\нОписаниеПроверки")
        выбросить новый ИсключениеНедопустимоеСостояние(ТекстОшибки)
    ;


;

@Локально
структура ОписаниеЗначения
	
	пер Значение: Объект? = Неопределено
    пер ТипЗначения: Тип? = Неопределено
	пер Представление: Строка? = Неопределено
	пер ИмяСвойства: Строка|Число? = Неопределено
    пер ПараметрыМетода: Массив<Объект?>? = Неопределено
	
    @Глобально
    метод Значение(ПроверяемоеЗначение: Объект?)

        Значение = ПроверяемоеЗначение
        ТипЗначения = Значение?.ПолучитьТип()
    ;

    @Глобально
    метод Значение(): Объект?
        выбор
        когда Значение == Неопределено
            возврат Неопределено
        когда Свойство() != Неопределено
            выбор
            когда ТипЗначения.ВСтроку().Найти("Массив") > 0
                возврат (Значение как Массив<Объект?>)[Свойство() как Число]
            когда ТипЗначения.Представление().НачинаетсяС("Map<")
                возврат (Значение как Соответствие<неизвестно, неизвестно>)[Свойство()]
            иначе
                возврат Значение.ПолучитьТип().ПолучитьСвойство(Свойство() как Строка)
            ;
            
        иначе
            возврат Значение
        ;
        
    ;
    @Глобально
    метод ТипЗначения(): Тип

        возврат ТипЗначения
    ;

    @Глобально
    метод Свойство(ПроверяемоеСвойство: Строка|Число?)
        ИмяСвойства = ПроверяемоеСвойство
    ;
    @Глобально
    метод Свойство(): Строка|Число?
        возврат ИмяСвойства == Неопределено ? Неопределено : ИмяСвойства
    ;

    @Глобально
    метод ПараметрыМетода(Параметр: Объект?)
        если ПараметрыМетода == Неопределено
            ПараметрыМетода = []
        ;
        ПараметрыМетода.Добавить(Параметр)
    ;
    @Глобально
    метод ПараметрыМетода(): Массив<Объект?>?
        возврат ПараметрыМетода
    ;
;


// СЛУЖЕБНЫЕ МЕТОДЫ

@Глобально
метод ЗначениеЗаполнено(ЗначениеПроверки: неизвестно): Булево
выбор ЗначениеПроверки 
    когда это Неопределено
        возврат Ложь
    когда это Булево
        возврат  Истина
    когда это Строка
        возврат  не (ЗначениеПроверки как Строка).Пусто()
    когда это Число
        возврат (ЗначениеПроверки как Число) != 0
    когда это Массив<неизвестно>
        возврат не (ЗначениеПроверки как Массив<неизвестно>).Пусто()
    когда это Соответствие<Объект, Объект?>
        возврат не (ЗначениеПроверки как Соответствие<Объект, Объект?>).Пусто()
    когда это Дата
        возврат (ЗначениеПроверки как Дата) != Дата{}
    иначе
        выбросить новый ИсключениеНедопустимоеСостояние("Передан неизвестный тип")
    ;
;