#требуется СтроковыеФункции.sbsl

// РАБОТА С КОЛЛЕКЦИЯМИ

@Глобально
исключение ИсключениеПриОбращенииКСвойствамКоллекции
    пер ТипКоллекции: Строка
    пер ИмяСвойства: Строка

    метод ПолучитьОписание(): Строка
        возврат "Попытка обращения к не существующиему свойству <$ИмяСвойства> для коллекции с типом <$ТипКоллекции>" 
    ;
    
;

@Глобально
метод ПолучитьЗначениеСвойства(Коллекция: Объект, Свойство: Строка, ВыбрасыватьИсключения: Булево = Ложь): Объект?
    пер ОписаниеСвойства: ОтражениеСвойства?
    попытка
        ОписаниеСвойства = Коллекция.ПолучитьТип().ПолучитьСвойство(Свойство)
    поймать Исключение: ИсключениеОтражения
        если ВыбрасыватьИсключения
            выбросить новый ИсключениеПриОбращенииКСвойствамКоллекции(
                Коллекция.ПолучитьТип().ВСтроку(),
                ИмяСвойства = Свойство)
        иначе
            возврат Неопределено    
        ;
        
    ;
    
    возврат ОписаниеСвойства.Получить(Коллекция)
;

@Глобально
метод УстановитьЗначениеСвойства(Коллекция: Объект, Свойство: Строка, Значение: неизвестно, ВыбрасыватьИсключения: Булево = Ложь): Булево
    пер Результат = Истина
    пер ОписаниеСвойства: ОтражениеСвойства?
    попытка
        ОписаниеСвойства = Коллекция.ПолучитьТип().ПолучитьСвойство(Свойство)
        ОписаниеСвойства.Установить(Коллекция, Значение)
    поймать Исключение: ИсключениеОтражения
        если ВыбрасыватьИсключения
            выбросить новый ИсключениеПриОбращенииКСвойствамКоллекции(
                Коллекция.ПолучитьТип().ВСтроку(),
                ИмяСвойства = Свойство)
        иначе
            Результат = Ложь
        ;
    ;
    
    возврат Результат
;

// Заменяет в шаблоне строки имена свойств коллекции на их значения.
//
// Параметры:
//  ШаблонСтроки - Строка    - строка, в которую необходимо вставить значения.
//  Коллекция    - Структура - подставляемые значения параметров, где ключ - имя свойства
//                             значение - вставляемое значение.
//
// Возвращаемое значение:
//  Строка - строка со вставленными значениями.
//
// Пример:
//  знч Коллекция = новый Структура(Фамилия = "Пупкин", Имя" ="Вася");
//  Результат = Коллекции.ВставитьПараметрыВСтроку("Здравствуй, \${Имя} \${Фамилия}.", Коллекция);
//  - возвращает: "Здравствуй, Вася Пупкин".
//
@Глобально
метод ВставитьПараметрыВСтроку(ШаблонСтроки: Строка, Коллекция: Объект): Строка
	пер Результат = ШаблонСтроки
    пер СвойстваКоллекции = Коллекция.ПолучитьТип().ПолучитьСвойства()

    для Свойство из СвойстваКоллекции
        пер ИмяСвойства = Свойство.Имя.ВСтроку()
        пер Значение = ПолучитьЗначениеСвойства(Коллекция, ИмяСвойства)
        выбор
        когда Значение == Неопределено
            продолжить
        когда Значение это Строка
            Значение = (Значение как Строка)
        когда Значение это Булево
            Значение = (Значение как Булево).Представление()
        когда Значение это Дата
            Значение = (Значение как Дата).Представление()
        когда (Значение.ПолучитьТип().ВСтроку().Найти("СтруктурыДанных.Свойство") > 0)
            Значение = ПолучитьЗначениеСвойства(Значение, "Значение")
            если Значение == Неопределено
                Значение = ""
            ;

        ;
        
        Результат = СтроковыеФункции.ВставитьПараметрыВСтроку(Результат, ИмяСвойства, (Значение как Строка))
        
    ;
	возврат Результат
;
