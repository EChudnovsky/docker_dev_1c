#требуется СтроковыеФункции.sbsl

@Глобально
перечисление УровниЛогирования
	DEBUG,
    INFO,
    WARNING,
    ERROR
    
;

@Глобально
структура ЖурналЛогирования
	знч УровеньЛогирования: УровниЛогирования = УровниЛогирования.INFO
    пер ТихийРежим				 = Ложь
	пер ВыводВКонсоль			 = Истина
	
	// Файл логирования
	пер КаталогЛогирования		 = "\${LOG_PATH_DIR}"
	пер ФайлЛога				 = "\${LOG_PATH_FILE}"
	пер ФайлЛогаРазделительСтрок = "\в\н" // возврат корретки, новая строка
	пер ФайлЛогаКодировка 		 = "UTF-8"
	
	// Служебный
	пер ПриоритетЛогирования: Число?

    @Глобально
	метод Инициализация(ПутьКаталогуЛога:Строка? = Неопределено, ПутьФайлуЛога: Строка? = Неопределено)
	
		// Установим уровень логирования
		выбор УровеньЛогирования
		когда УровниЛогирования.DEBUG
			ПриоритетЛогирования = 0
		когда УровниЛогирования.INFO
			ПриоритетЛогирования = 10
		когда УровниЛогирования.WARNING
			ПриоритетЛогирования = 20
		когда УровниЛогирования.ERROR
			ПриоритетЛогирования = 99
		иначе
		;
		
		пер ПеременныеОС = СредаИсполнения.ПолучитьВсеПеременные()
		если ПутьКаталогуЛога != Неопределено 
			КаталогЛогирования = ПутьКаталогуЛога
		иначе если КаталогЛогирования == "\${LOG_PATH_DIR}"
			КаталогЛогирования = ПеременныеОС.ПолучитьИлиУмолчание("LOG_PATH_DIR", "")
		;

		если ПутьФайлуЛога != Неопределено
			ФайлЛога = ПутьФайлуЛога
		иначе если ФайлЛога == "\${LOG_PATH_FILE}"
			ФайлЛога = ПеременныеОС.ПолучитьИлиУмолчание("LOG_PATH_FILE", "")
		;
	;

	// Выводит отладочное сообщение
	@Глобально
	метод Отладка(Сообщение: Строка, ВременнаяМетка: Булево = Истина)
		Записать(УровниЛогирования.DEBUG, Сообщение, 0, ВременнаяМетка)
	;

	// Выводит информационное сообщение
	@Глобально
	метод Информация(Сообщение: Строка, ВременнаяМетка: Булево = Истина)
		Записать(УровниЛогирования.INFO, Сообщение, 10, ВременнаяМетка)
	;

	// Выводит предупреждение
	@Глобально
	метод Предостережение(Сообщение: Строка)
		Записать(УровниЛогирования.WARNING, Сообщение, 20)
	;

	// Выводит сообщение об ошибке
	@Глобально
	метод Ошибка(Сообщение: Строка, Ошибка:Объект? = Неопределено)
		
		Сообщение = ПредставлениеОшибки(Сообщение, Ошибка)
		Записать(УровниЛогирования.ERROR, Сообщение, 99)
		
	;

	метод Записать(УровеньЛога: УровниЛогирования, Сообщение: Строка, Приоритет: Число, ВременнаяМетка: Булево = Истина)

		если не ЛогированиеВключено(Приоритет) 
			возврат
		;
		
		пер Сообщения = [ ВременнаяМетка ? "${ДатаВремя.Сейчас()|дд.ММ.гггг ЧЧ:мм:сс.С} [$УровеньЛога]: $Сообщение" : "$Сообщение"]

		если ВыводВКонсоль
			ЗаписатьЛогВКонсоль(Сообщения)
		;
		
		если не ФайлЛога.Пусто()
			ЗаписатьЛогВФайл([КаталогЛогирования, ФайлЛога], Сообщения)
		;

	;

	метод ЗаписатьЛогВКонсоль(Сообщения: Массив<Строка>)
		для Сообщение из Сообщения
			Консоль.Записать(Сообщение)
		;		
	;

	метод ЗаписатьЛогВФайл(ПутькФайлуЛогирования: Массив<Строка>, Сообщения: Массив<Строка>, ЗаписатьВКонец: Булево = Истина)

		попытка

			пер ФайлЛогирования = новый Файл("", ПутькФайлуЛогирования)
			если не ФайлЛогирования.Существует()
				ФайлЛогирования = Файлы.Создать(новый Файл("", ПутькФайлуЛогирования))
			;		
			
			исп ПотокЗаписи = ФайлЛогирования.ОткрытьПотокЗаписи(ЗаписатьВКонец)

		    // Настройки записи с разделителем CR LF и кордировки Windows-1251
			знч НастройкиЗаписи 			 = новый НастройкиЗаписиДанных()
			НастройкиЗаписи.РазделительСтрок = ФайлЛогаРазделительСтрок
			НастройкиЗаписи.Кодировка 		 = ФайлЛогаКодировка
			
			знч ЗаписьДанных = новый ЗаписьДанных(ПотокЗаписи, НастройкиЗаписи)
			
			Сообщения.ДляКаждого(Строка -> ЗаписьДанных.ЗаписатьСтроку(Строка))

		поймать ИнформацияОбОшибке: Исключение
			Сообщения.Очистить()
			Сообщения.Добавить(ПредставлениеОшибки("Вывод сообщения в файл: <$ФайлЛога>", ИнформацияОбОшибке))
			ЗаписатьЛогВКонсоль(Сообщения)
		;
		
	;

	метод ЛогированиеВключено(Приоритет: Число): Булево

		если ТихийРежим 
			возврат Ложь
		иначе если ПриоритетЛогирования == Неопределено
			Инициализация()
		;		
		возврат Приоритет >= ПриоритетЛогирования		
	;

	метод ПредставлениеОшибки(ОписаниеОшибки: Строка, Ошибка:Объект? = Неопределено): Строка
		
		пер ЧастиСтрок: Массив<Объект?> = [ОписаниеОшибки]
		выбор Ошибка
		когда Неопределено
		иначе
			ЧастиСтрок.Добавить(Ошибка) 
			
		;

		возврат Строки.Соединить(ЧастиСтрок, ": ")
	;

; 

