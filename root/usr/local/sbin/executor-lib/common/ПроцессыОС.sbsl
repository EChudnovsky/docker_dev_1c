#требуется Логирование.sbsl

@Глобально
структура ОписаниеПроцессаОС
    
    // Параметры выполнения
    обз знч Команда: Строка
    обз знч Параметры: ЧитаемыйМассив<Объект>
    пер ЖурналЛогирования: Логирование.ЖурналЛогирования?

    // Процесс
    пер Процесс: ПроцессОс?
    пер pid: Число?
    пер ВремяОжидания: Длительность = 60с
    пер КодВозврата: Число
    
    // Результат
    пер ПотокВывода: Массив<Строка>
    пер ПотокОшибок: Массив<Строка>

    @Глобально
    метод Запустить()
        
        если ЖурналЛогирования == Неопределено
            ЖурналЛогирования = новый Логирование.ЖурналЛогирования()
            ЖурналЛогирования.Инициализация()
        ;
        Процесс = новый ПроцессОс(
            Команда = Команда,
            Аргументы = Параметры,
            СоединитьПотокиОшибокИВывода = Ложь)

        Процесс.Запустить()
        pid = Процесс.ПолучитьPid()
        если pid == Неопределено
            КодВозврата = -1
            ВывестиПоток(
                "",
                "Не удалось запустить процесс:
                    Команда: $Команда ${Строки.Соединить(Параметры, " ")}", Истина)
        иначе
            ЖурналЛогирования.Отладка(
            "ЗАПУЩЕН ПРОЦЕСС:
                Команда: $Команда ${Строки.Соединить(Параметры, " ")}
                pid: $pid")
        ;

        // Выходим без ожидания
        если ВремяОжидания <= 0с
            возврат        
        ;

        исп РезультатПотокаВывода = Процесс.ПолучитьПотокВывода()
        исп РезультатПотокаОшибок = Процесс.ПолучитьПотокОшибок()

        пока не Процесс.ОжидатьЗавершения(ВремяОжидания)
            ВывестиПоток(
                РезультатПотокаВывода.ПрочитатьКакСтроку(), 
                РезультатПотокаОшибок.ПрочитатьКакСтроку(), 
                Истина)
        ;

        ВывестиПоток(
            РезультатПотокаВывода.ПрочитатьКакСтроку(), 
            РезультатПотокаОшибок.ПрочитатьКакСтроку(), 
            Истина)

        КодВозврата = Процесс.ПолучитьКодВозврата()
        если КодВозврата == 0
             ЖурналЛогирования.Отладка("ПРОЦЕСС ЗАВЕРШЕН. УСПЕХ")
        иначе
            ЖурналЛогирования.Отладка("ПРОЦЕСС ВЕРНУЛ КОД ОШИБКИ: $КодВозврата")
        ;
                
    ;
    
    @Глобально
    метод Остановить()

        Процесс.Остановить()
        КодВозврата = -1
        ВывестиПоток( "", "Процесс прерван.")

    ;
    
    @Глобально
    метод Живой(): Булево
        возврат Процесс.Живой()
    ;
    
    @Локально
    метод ВывестиПоток(ТекстВывода: Строка, ТекстОшибки: Строка = "", ЧтениеПотоков: Булево = Ложь)
        
        если не СтроковыеФункции.ПустаяСтрока(ТекстВывода)
            ЖурналЛогирования.Информация("$ТекстВывода")
            если ЧтениеПотоков
                ПотокВывода.Добавить(ТекстВывода)
            ;
        ;

        если не СтроковыеФункции.ПустаяСтрока(ТекстОшибки)
            ЖурналЛогирования.Ошибка("$ТекстОшибки")
            если ЧтениеПотоков
                ПотокОшибок.Добавить(ТекстОшибки)
            ;
        ;
    ;    
;